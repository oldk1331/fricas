--Copyright The Numerical Algorithms Group Limited 1994.
-- Color gallery page 6

-- 3-d surface whose height is the real part of the Gamma function,
-- and whose color is the argument of the Gamma function
gam(x,y) ==
  g := Gamma complex(x,y)
  point [x,y,max(min(real g, 4), -4), argument g]

vp := draw(gam, -%pi..%pi, -%pi..%pi, title == "Gamma(x + %i*y)",
     var1Steps == 100, var2Steps == 100)
axes(vp, "off")
write(vp, "image6-1", "postscript")

-- The Beta function

b(x,y) == 0.025 * Beta(x,y)
vp := draw(b, -3.1..3, -3.1 .. 3, title == "Beta(x,y)")
axes(vp, "off"); drawStyle(vp, "smooth"); translate(vp, 0, -30); zoom(vp, 2.5)
write(vp, "image6-2", "postscript")

-- The complex arc tangent function
atf(x,y) ==
  a := atan complex(x,y)
  point [x,y,real a, argument a]

vp := draw(atf, -3.0..%pi, -3.0..%pi)
write(vp, "image6-3", "postscript")
